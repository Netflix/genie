plugins {
    id "com.moowork.node" version "1.2.0"
}

apply plugin: "org.asciidoctor.convert"

project.parent.tasks.collectDocumentation.dependsOn project.tasks.asciidoctor

ext {
    snippetsDir = new File(project.buildDir, "generated-snippets")
}

dependencies {
    /*******************************
     * Compile Dependencies
     *******************************/

    compile(project(":genie-core"))

    // Swagger libs
    compile("io.springfox:springfox-swagger2")
    compile("io.springfox:springfox-swagger-ui")
    compile("io.springfox:springfox-bean-validators")

    // Commons
    compile("org.apache.commons:commons-exec")
    compile("org.apache.httpcomponents:httpclient")
    compile("commons-validator:commons-validator")

    // JWT JOSE implementation lib
    compile("org.bitbucket.b_c:jose4j")

    // Flyway for database migrations
    compile("org.flywaydb:flyway-core")

    // Spring Libs
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-data-redis")
    compile("org.springframework.boot:spring-boot-starter-mail")
    compile("org.springframework.boot:spring-boot-starter-hateoas")
    compile("org.springframework.boot:spring-boot-starter-integration")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-starter-tomcat")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.integration:spring-integration-zookeeper")
    compile("org.springframework.cloud:spring-cloud-starter-eureka")
    compile("org.springframework.cloud:spring-cloud-starter-spectator")
    compile("org.springframework.security:spring-security-jwt")
    compile("org.springframework.security.extensions:spring-security-saml2-core") {
        exclude group: "org.bouncycastle", module: "bcprov-jdk15"
    }
    compile("org.springframework.security.oauth:spring-security-oauth2")
    compile("org.springframework.session:spring-session")

    /*******************************
     * Provided Dependencies
     *******************************/

    /*******************************
     * Optional Dependencies
     *******************************/

    compileOnly("org.springframework.boot:spring-boot-configuration-processor")

    /*******************************
     * Runtime Dependencies
     *******************************/

    runtime("org.codehaus.groovy:groovy-all")

    /*******************************
     * Test Dependencies
     *******************************/

    testCompile(project(":genie-test"))
    testCompile("com.github.springtestdbunit:spring-test-dbunit")
    testCompile("com.github.tomakehurst:wiremock")
    testCompile("com.jayway.jsonpath:json-path")
    testCompile("net.sf.jtidy:jtidy")
    testCompile("org.dbunit:dbunit")
    testCompile("org.springframework.boot:spring-boot-starter-jetty")
    testCompile("org.springframework.restdocs:spring-restdocs-core")
    testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")
    testCompile("org.springframework.security:spring-security-test")
}

license {
    excludes(["static/*", "templates/*", "*.yml", "genie-banner.txt", "db/**/*.sql"])
}

test {
    outputs.dir snippetsDir
}

// This is needed cause some things like attributes for asciidoctor don't serialize project.version properly at runtime
def genieVersion = project.version.toString()

asciidoctor {
    dependsOn test
    inputs.dir snippetsDir
    attributes  \
             "snippets": snippetsDir,
            "revnumber": genieVersion,
            "docinfo1": ""

    // See: https://github.com/asciidoctor/asciidoctor-gradle-plugin/issues/154
    def capturedOutput = []
    def listener = { capturedOutput << it } as StandardOutputListener

    logging.addStandardErrorListener(listener)
    logging.addStandardOutputListener(listener)

    doLast {
        logging.removeStandardOutputListener(listener)
        logging.removeStandardErrorListener(listener)
        capturedOutput.join('').with { output ->
            if (output =~ /include file not found:/) {
                throw new RuntimeException("Include file(s) not found.\n" + output)
            }
        }
    }
}

processResources {
    from("src/main/resources") {
        include "**/*.properties"
        include "**/*.yml"
        filter {
            it.replace("GENIE_VERSION", genieVersion)
        }
    }
}

compileJava {
    dependsOn processResources
}

node {
    version = "4.4.0"
    npmVersion = "3.8.6"
    download = true
}

def static_build_dir = new File(project.projectDir, "/src/main/resources/static/build")
def static_project_dir = new File(project.projectDir, "/src/main/resources/static")

clean {
    delete static_build_dir
}

task bundle(type: NpmTask) {
    inputs.files(fileTree("node_modules"))
    inputs.file("npm-shrinkwrap.json")
    inputs.file("package.json")
    inputs.file("server.js")
    inputs.file("webpack.config.js")
    inputs.dir static_project_dir

    outputs.dir static_build_dir

    dependsOn npm_install
    args = ["run", "build"]
}

npm_install.args = ["--loglevel", "error"]
npmInstall.args = ["--loglevel", "error"]

jar {
    dependsOn bundle

    manifest {
        attributes("Implementation-Version": genieVersion)
    }

    from(static_build_dir) {
        into "static"
    }
}
