import com.github.spotbugs.SpotBugsTask
import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        jcenter()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.netflix.nebula:gradle-aggregate-javadocs-plugin:3.0.1"
    }
}

plugins {
    id "com.github.kt3k.coveralls" version "2.9.0"
    id "nebula.netflixoss" version "8.4.4"
    id "org.ajoberstar.grgit" version "3.1.1"
    id "org.ajoberstar.git-publish" version "2.1.1"
    id "org.springframework.boot" version "${spring_boot_version}" apply false
    id "org.asciidoctor.jvm.convert" version "2.4.0" apply false
    id "com.gorylenko.gradle-git-properties" version "2.0.0" apply false
    id "com.google.protobuf" version "0.8.8" apply false
    id "io.franzbecker.gradle-lombok" version "3.2.0" apply false
    id "nebula.node" version "1.3.0" apply false
    id "com.github.spotbugs" version "3.0.0" apply false
}

apply plugin: "nebula-aggregate-javadocs"

ext.githubProjectName = rootProject.name

def javaProjects = subprojects.findAll {
    it.name != "genie-demo" && it.name != "genie-ddl" && it.name != "genie-docs"
}

allprojects {
    apply plugin: "jacoco"
    apply plugin: "idea"
    apply plugin: "eclipse"

    repositories {
        jcenter()
    }
}

idea {
    project {
        jdkName = "1.8"
        languageLevel = "8"
        vcs = "Git"
    }
}

configure(javaProjects) {
    apply plugin: "nebula.netflixoss"
    apply plugin: "java"
    apply plugin: "checkstyle"
    apply plugin: "com.github.spotbugs"
    apply plugin: "pmd"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.gorylenko.gradle-git-properties"
    apply plugin: "groovy"
    apply plugin: "io.franzbecker.gradle-lombok"
    apply plugin: "nebula.facet"

    group = "com.netflix.${githubProjectName}"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    facets {
        smokeTest {
            parentSourceSet = "test"
            // includeInCheckLifecycle = false
        }
        integTest {
            parentSourceSet = "test"
            testTaskName = "integrationTest"
            // includeInCheckLifecycle = false
        }
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}"
            mavenBom "org.spockframework:spock-bom:1.3-groovy-2.5"
        }
        dependencies {
            dependency("com.beust:jcommander:1.72")
            dependency("cglib:cglib-nodep:3.2.12")
            dependency("com.github.fge:json-patch:1.9")
            dependency("com.github.springtestdbunit:spring-test-dbunit:1.3.0")
            dependency("com.github.spotbugs:spotbugs-annotations:3.1.12")
            dependencySet(group: "com.squareup.okhttp3", version: "3.11.0") {
                entry "okhttp"
                entry "mockwebserver"
            }
            dependencySet(group: "com.squareup.retrofit2", version: "2.6.1") {
                entry "retrofit"
                entry "converter-jackson"
            }
            dependency("com.google.code.findbugs:annotations:3.0.1")
            dependency("com.google.guava:guava:20.0")
            dependencySet(group: "com.google.protobuf", version: "3.6.1") {
                entry "protobuf-java"
                entry "protoc"
            }
            dependency("commons-beanutils:commons-beanutils:1.9.3")
            dependency("commons-httpclient:commons-httpclient:3.1")
            dependency("commons-io:commons-io:2.6")
            dependency("commons-validator:commons-validator:1.6")
            dependencySet(group: "io.grpc", version: "1.10.1") {
                entry "grpc-core"
                entry "grpc-netty"
                entry "grpc-stub"
                entry "grpc-protobuf"
                entry "protoc-gen-grpc-java"
                entry "grpc-testing"
            }
            dependencySet(group: "io.springfox", version: "2.9.2") {
                entry "springfox-swagger2"
                entry "springfox-swagger-ui"
                entry "springfox-bean-validators"
            }
            dependency("javax.el:javax.el-api:3.0.1-b06")
            dependency("javax.persistence:javax.persistence-api:2.2")
            dependency("net.sf.jtidy:jtidy:r938")
            dependency("net.jcip:jcip-annotations:1.0")
            dependency("org.apache.commons:commons-configuration2:2.5")
            dependency("org.apache.commons:commons-exec:1.3")
            dependency("org.apache.commons:commons-text:1.7")
            dependency("org.apache.tika:tika-core:1.20")
            dependency("org.apache.maven:maven-artifact:3.6.1")
            // For some reason 3.5.3-beta was throwing error with 3.4.x server
            // https://issues.apache.org/jira/browse/ZOOKEEPER-2775
            // https://curator.apache.org/zk-compatibility.html
            dependency("org.apache.zookeeper:zookeeper:3.4.12")
            dependency("org.bitbucket.b_c:jose4j:0.6.3")
            // Looks like as of Boot 2.1.x this dependency isn't managed by them anymore. This matches whats available
            // in Spring per
            // https://github.com/spring-projects/spring-boot/blob/2.1.x/spring-boot-project/spring-boot-dependencies/pom.xml#L67
            dependency("org.codehaus.groovy:groovy-all:2.5.7")
            dependency("org.codehaus.janino:janino:3.0.14")
            dependency("org.dbunit:dbunit:2.5.4")
            dependencySet(group: "org.jruby", version: "9.1.14.0") {
                entry "jruby-complete"
                entry "jruby-core"
                entry "jruby-stdlib"
            }
            dependency("org.opensaml:opensaml:2.6.4")
            dependency("org.python:jython-standalone:2.7.1")
            // Attempt to update this to 2.1.3 caused tests to fail - TJG 7/8/2019
            // StateMachineAutoConfigurationTest. cancelAfterAgentConfigure
            // StateMachineAutoConfigurationTest. cancelAfterSetup
            dependencySet(group: "org.springframework.statemachine", version: "2.1.1.RELEASE") {
                entry "spring-statemachine-core"
                entry "spring-statemachine-test"
            }
        }
    }

    ext {
        generatedSourcesDir = file("${buildDir}/generated-sources/main")
        generatedSourcesJavaDir = file(new File(generatedSourcesDir, "/java"))
        generatedSourcesGRPCDir = file("${buildDir}/generated-grpc")
        generatedSourcesGRPCJavaDir = file(new File(generatedSourcesGRPCDir, "/main/java"))
    }

    sourceSets {
        main {
            java {
                srcDir generatedSourcesJavaDir
                srcDir generatedSourcesGRPCJavaDir
            }
        }
    }

    idea.module {
        // Exclude JPA metamodels, as IDEA generates its own
        sourceDirs -= generatedSourcesJavaDir
    }

    configurations {
        all*.exclude group: "javax.servlet", module: "servlet-api"
    }

    dependencies {
        /*******************************
         * Annotation Processors
         *******************************/

        annotationProcessor("net.jcip:jcip-annotations")
        annotationProcessor("com.github.spotbugs:spotbugs-annotations")

        /*******************************
         * Compile Dependencies
         *******************************/

        /*******************************
         * Provided Dependencies
         *******************************/

        compileOnly("com.google.code.findbugs:annotations")

        /*******************************
         * Runtime Dependencies
         *******************************/

        /*******************************
         * Test Dependencies
         *******************************/

        testAnnotationProcessor("net.jcip:jcip-annotations")
        testAnnotationProcessor("com.github.spotbugs:spotbugs-annotations")

        testCompile("cglib:cglib-nodep")
        testCompile("org.spockframework:spock-core")
        testCompile("org.spockframework:spock-spring")
        testCompile("org.springframework.boot:spring-boot-starter-test")
        testCompile("org.junit.jupiter:junit-jupiter-api")
        testCompile("org.junit.jupiter:junit-jupiter-params")

        testCompileOnly("com.google.code.findbugs:annotations")

        testRuntimeOnly("javax.el:javax.el-api")
        testRuntimeOnly("org.glassfish:javax.el")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
        testRuntimeOnly("org.junit.vintage:junit-vintage-engine")
    }

    task removeGeneratedSources(type: Delete) {
        generatedSourcesDir.deleteDir()
    }

    task removeIdeaOutDirectory(type: Delete) {
        file("${projectDir}/out").deleteDir()
    }

    clean {
        dependsOn(removeIdeaOutDirectory)
    }

    compileJava {
        doFirst {
            generatedSourcesDir.mkdirs()
            generatedSourcesJavaDir.mkdirs()
            generatedSourcesGRPCJavaDir.mkdirs()
        }
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        options.compilerArgs += ['-s', generatedSourcesJavaDir]
    }

    compileTestJava {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    tasks.withType(Test) {
        task ->
            // Use JUnit 5 with Jupiter
            useJUnitPlatform()
            // set heap size for the test JVM(s)
            minHeapSize = "256m"
            maxHeapSize = "2g"

            // Environment properties to be forwarded to the test runtime
            [
                    "CI",
                    "INTEGRATION_TEST_DB",
            ].each { envVariableName ->
                if (System.getenv(envVariableName) != null) {
                    systemProperty envVariableName, System.getenv(envVariableName)
                }
            }

            if (System.env."TRAVIS" == "true") {
                systemProperty "spring.profiles.include", "travisci"
            }

            reports.html.setDestination(new File("${reporting.baseDir}/test/${task.name}"))
            jacocoTestReport.getExecutionData().plus(files("$buildDir/jacoco/${task.name}.exec"))

            // Customize test logging on CI and everywhere else
            testLogging {
                if (System.getenv("CI") != null) {
                    events "passed", "skipped", "failed"
                    exceptionFormat "full"
                } else {
                    events "failed"
                }
            }
    }

    license {
        ignoreFailures false
    }

    checkstyle {
        toolVersion = "8.27"
    }

    spotbugs {
        excludeFilter = new File("$rootProject.projectDir/config/spotbugs/excludeFilter.xml")
    }
    spotbugsTest.enabled = false
    spotbugsIntegTest.enabled = false
    spotbugsSmokeTest.enabled = false

    tasks.withType(SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    pmd {
        // Gradle 5 upgraded PMD to 6.7 (https://github.com/gradle/gradle/issues/6625)
        // This caused all kinds of errors in our build. Pin back for now and upgrade at our own pace or look further
        toolVersion = "5.6.1"
    }

    tasks.withType(Pmd) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }
}

/**********************************
 * Coverage Tasks
 **********************************/

task codeCoverageReport(type: JacocoReport, group: "Coverage reports") {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    dependsOn javaProjects*.test

    javaProjects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination new File("${buildDir}/reports/jacoco/report.xml")
        html.enabled = true
        html.destination new File("${buildDir}/reports/jacoco/html")
        csv.enabled = false
    }
    afterEvaluate {
        // Exclude generated files from top-level coverage report
        classDirectories.setFrom(
                files(
                        classDirectories.files.collect {
                            fileTree(
                                    dir: it,
                                    exclude: [
                                            "**/com/netflix/genie/proto/**",
                                            "**/com/netflix/genie/web/data/entities/*_*",
                                    ]
                            )
                        }
                )
        )
    }
}

coveralls {
    sourceDirs = javaProjects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${project.buildDir}/reports/jacoco/report.xml"
}

tasks.coveralls {
    group = "Coverage reports"
    description = "Uploads the aggregated coverage report to Coveralls"

    dependsOn codeCoverageReport
    onlyIf {
        System.env."CI"
    }
}

/**********************************
 * Github Pages Tasks
 **********************************/

task collectDocumentation(group: "documentation", description: "Copy the documentation from sub-projects") {
    dependsOn "aggregateJavadocs"

    def restDocs = new File(project(":genie-web").buildDir, "docs/asciidoc")
    def referenceDocs = new File(project(":genie-docs").buildDir, "docs/asciidoc")
    def demoDocs = new File(project(":genie-demo").buildDir, "docs/asciidoc")
    def dockerCompose = new File(project(":genie-demo").projectDir, "src/main/docker/docker-compose.yml")
    def docsDir = new File(project.buildDir, "docs")

    inputs.dir restDocs
    inputs.dir referenceDocs
    inputs.dir demoDocs
    inputs.file dockerCompose
    outputs.dir docsDir

    doLast {
        copy {
            from dockerCompose
            into new File(docsDir, "demo")
            filter {
                it.replace('${GENIE_VERSION}', project.version.toString())
            }
        }
        copy {
            from restDocs
            into new File(docsDir, "rest")
        }
        copy {
            from referenceDocs
            into new File(docsDir, "reference")
        }
        copy {
            from demoDocs
            into new File(docsDir, "demo")
        }
    }
}

gitPublish {

    branch = "gh-pages"

    contents {
        into("docs/${project.version}/") {
            from "${project.buildDir}/docs/"
        }
        into("_releases") {
            from new File(project(":genie-docs").projectDir, "src/templates/releaseTemplate.md")
            rename {
                String fileName -> fileName.replace("releaseTemplate", "${project.version}")
            }
            filter {
                it.replace("GENIE_VERSION", project.version.toString())
            }
        }
    }

    preserve {
        include "**/*"
        exclude "docs/${project.version}/"
    }

    commitMessage = "Documentation generated for ${project.version} by Travis Build ${System.env.TRAVIS_BUILD_NUMBER}".toString()
}

gitPublishCopy {
    // This also depends on asciidoctor task for some sub-projects but that dependency is reverse mapped in the
    // pertinent sub-project build file
    dependsOn tasks.collectDocumentation
}

gitPublishPush {
    onlyIf {
        System.env."CI"
    }
}

/**********************************
 * Docker Tasks
 **********************************/

def isLatest() {
    return (boolean) !project.version.toString().contains("SNAPSHOT") && !project.version.toString().contains("-rc.")
}

def getDockerTags(String appName) {
    def tags = ["netflixoss/${appName}:${project.version}"]
    if (isLatest()) {
        tags.add("netflixoss/${appName}:latest")
    }
    return tags
}

task dockerLogout(type: Exec, group: "Docker", description: "Logout of docker hub") {
    commandLine "docker", "logout"
}

task dockerLogin(type: Exec, group: "Docker", description: "Login to docker hub using DOCKER_USER and DOCKER_PASSWORD environment variables") {
    dependsOn tasks.dockerLogout
    commandLine "docker", "login", "-u", System.getenv("DOCKER_USER"), "-p", System.getenv("DOCKER_PASSWORD")
}

task dockerBuildAllImages(group: "Docker", description: "Container task for all docker image builds") {
    dependsOn ":genie-app:dockerBuildAppImage"
    dependsOn ":genie-demo:dockerBuildApacheImage"
    dependsOn ":genie-demo:dockerBuildClientImage"
}
