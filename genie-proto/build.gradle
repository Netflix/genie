apply plugin: "java-library"
apply plugin: "com.google.protobuf"

ext {
    protoIgnorePattern = "**/com/netflix/genie/proto/*"
}

dependencies {
    /*******************************
     * API Dependencies
     *******************************/

    api("com.google.protobuf:protobuf-java")
    api("com.google.protobuf:protobuf-java-util")

    /*******************************
     * Implementation Dependencies
     *******************************/

    implementation("io.grpc:grpc-stub")
    implementation("io.grpc:grpc-protobuf")

    /*******************************
     * Compile Only Dependencies
     *******************************/

    /*******************************
     * Runtime Only Dependencies
     *******************************/

    /*******************************
     * Test Dependencies
     *******************************/

    testImplementation(project(":genie-test"))
}

// Configure protobuf classes and grpc stubs generation
protobuf {
    protoc {
        // Download the protoc compiler
        // This particular protobuf dependency isn't in the BOM for some reason
        // workaround for no native M1 support https://github.com/grpc/grpc-java/issues/7690#issuecomment-760332746
        if (osdetector.os == "osx") {
            artifact = "com.google.protobuf:protoc:${protobuf_version}:osx-x86_64"
        } else {
            artifact = "com.google.protobuf:protoc:${protobuf_version}"
        }
        // Use project convention for generated files
        generatedFilesBaseDir = project.generatedSourcesDir
    }

    plugins {
        grpc {
            // Download the grpc compiler
            if (osdetector.os == "osx") {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}:osx-x86_64"
            } else {
                artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
            }
        }
    }

    generateProtoTasks {
        ofSourceSet("main")*.plugins {
            // Activate the gRPC plugin
            grpc {
                outputSubDir = "java"
            }
        }
    }
}

//Ignore the generated classes from all our style/bug/license/doc tools

tasks.withType(Checkstyle) {
    exclude protoIgnorePattern
}

tasks.withType(Pmd) {
    exclude protoIgnorePattern
}

license {
    exclude protoIgnorePattern
}

javadoc {
    exclude protoIgnorePattern
}

// Generate proto files before generating the IDEA project
ideaModule.dependsOn "generateProto"
ideaModule.dependsOn "generateTestProto"
