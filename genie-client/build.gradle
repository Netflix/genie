dependencies {
    /*******************************
     * Compile Dependencies
     *******************************/

    compile project(":genie-common")

    compile("commons-beanutils:commons-beanutils")
    compile("com.squareup.okhttp3:okhttp")
    compile("com.squareup.retrofit2:retrofit")
    compile("com.squareup.retrofit2:converter-jackson")
    compile("org.apache.commons:commons-configuration2")

    // Logging
    compile("org.slf4j:slf4j-api")

    // JSON Patch Support
    compile("com.github.fge:json-patch")

    /*******************************
     * Provided Dependencies
     *******************************/

    /*******************************
     * Runtime Dependencies
     *******************************/

    /*******************************
     * Test Dependencies
     *******************************/

    testCompile(project(":genie-test"))
    testCompile(project(":genie-web"))

    testCompile("org.springframework.boot:spring-boot-starter-jetty")
    testCompile("org.springframework.cloud:spring-cloud-contract-wiremock")
}

clean {
    delete "src/main/python/nflx_genie_client.egg-info"
    delete "src/main/python/dist"
}

task installPythonDependencies(type: Exec) {
    dependsOn clean
    workingDir "src/main/python"
    commandLine "sudo", "pip", "install", "-U", "pip", "setuptools", "twine"
}

task installPythonPackage(type: Exec) {
    dependsOn installPythonDependencies
    workingDir "src/main/python"
    commandLine "sudo", "pip", "install", "-U", "-e", "."
}

task testPythonPackage(type: Exec) {
    dependsOn installPythonPackage
    workingDir "src/main/python"
    commandLine "sh", "-c", "nosetests"
}

task buildPythonPackage(type: Exec) {
    dependsOn clean
    workingDir "src/main/python"
    commandLine "python", "setup.py", "sdist"
}

task uploadToPypi(type: Exec) {
    dependsOn buildPythonPackage
    workingDir "src/main/python"
    commandLine "bash", "-c", "twine upload dist/*"
}
