ext {
    metaModelIgnorePattern = "**/com/netflix/genie/core/jpa/entities/*_.java"
}

configurations {
    jpaModelGen
}

dependencies {
    /*******************************
     * Compile Dependencies
     *******************************/

    compile project(":genie-common")

    // JSON Patch Support
    compile("com.github.fge:json-patch")

    // Commons Libs
    compile("commons-httpclient:commons-httpclient")
    compile("commons-io:commons-io")
    compile("org.apache.commons:commons-exec:${commons_exec_version}")

    // Spring Libs
    compile("org.springframework.boot:spring-boot-starter-aop")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-mail")
    compile("org.springframework.retry:spring-retry")
    compile("org.springframework.cloud:spring-cloud-aws-context:${spring_aws_context_version}")
//    compile("org.springframework.cloud:spring-cloud-starter-spectator:${spring_cloud_netflix_version}")

    // AWS Libs
    compile("com.amazonaws:aws-java-sdk-sts:${aws_java_sdk_sts_version}")


    /*******************************
     * Provided Dependencies
     *******************************/

    /*******************************
     * Runtime Dependencies
     *******************************/

    // Database Libs
    runtime("mysql:mysql-connector-java")
    runtime("org.hsqldb:hsqldb")
    runtime("org.postgresql:postgresql")

    /*******************************
     * Test Dependencies
     *******************************/

    testCompile(project(":genie-test"))
    testCompile("org.dbunit:dbunit:${dbunit_version}")
    testCompile("com.github.springtestdbunit:spring-test-dbunit:${spring_test_dbunit_version}")

    /*******************************
     * JPA Model Gen Dependencies
     *******************************/

    // Hibernate Metamodel Generator Libraries
    jpaModelGen("org.hibernate:hibernate-jpamodelgen")
}

task generateJPAMetaModel(type: JavaCompile, group: 'build', description: 'Generate JPA Meta Models') {
    source = sourceSets.main.java
    outputs.dir generatedSourcesJavaDir
    destinationDir = generatedSourcesJavaDir
    dependencyCacheDir = compileJava.dependencyCacheDir
    classpath = configurations.compile + configurations.jpaModelGen
    options.compilerArgs = [
            "-proc:only"
    ]
}

compileJava {
    dependsOn generateJPAMetaModel
}

//Ignore the generated classes from all our style/coverage/bug finding tools
tasks.withType(Checkstyle) {
    exclude metaModelIgnorePattern
}

tasks.withType(FindBugs) {
    classes = classes.filter {
        !it.path.contains('_')
    }
}

tasks.withType(Pmd) {
    exclude metaModelIgnorePattern
}

test {
    jacoco {
        excludes = ['com/netflix/genie/core/jpa/entities/*_*']
    }
}

jacocoTestReport {
    doFirst {
        classDirectories = fileTree(dir: "${buildDir}/classes/main/").exclude(['com/netflix/genie/core/jpa/entities/*_*'])
    }
}
